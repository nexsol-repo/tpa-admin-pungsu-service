plugins {
    id 'java-library'
    id 'org.springframework.boot' apply(false)
    id 'io.spring.dependency-management'
    id 'io.spring.javaformat' apply(false)
    id 'org.asciidoctor.jvm.convert' apply(false)
}

apply from: 'lint.gradle'

allprojects {
    group = "${projectGroup}"
    version = "${applicationVersion}"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.asciidoctor.jvm.convert'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependenciesVersion}"
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.42")
        annotationProcessor("org.projectlombok:lombok:1.18.42")
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation 'org.springframework.boot:spring-boot-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.assertj:assertj-core'
        testImplementation 'org.mockito:mockito-core'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(project.javaVersion)
        }
    }

    bootJar.enabled = false
    jar.enabled = true

    test {
        useJUnitPlatform {
            excludeTags 'develop', 'restdocs'
        }
    }

    testing {
        suites {
            test {
                targets {
                    unitTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                excludeTags 'develop', 'context', 'restdocs'
                            }
                        }
                    }

                    contextTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                includeTags 'context'
                            }
                        }
                    }

                    restDocsTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                includeTags 'restdocs'
                            }
                        }
                    }

                    developTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                includeTags 'develop'
                            }
                        }
                    }
                }
            }
        }
    }
}

// RestDocs 전용 모듈 설정
// 1. 문서 생성 모듈 (오케스트레이션)
project(':tests:api-docs') {
    apply plugin: 'org.asciidoctor.jvm.convert'

    ext {
        snippetsDir = layout.buildDirectory.dir('generated-snippets')
    }

    test {
        outputs.dir snippetsDir
    }

    tasks.named('asciidoctor') {
        inputs.dir snippetsDir
        dependsOn restDocsTest

        sources {
            include '**/index.adoc'
        }

        baseDirFollowsSourceDir()
    }
}

// 2. 실제 배포 모듈: core-api
project(':core:core-api') {
    bootJar {
        def apiDocsProject = project(':tests:api-docs')
        // 빌드 전 문서 생성 선행
        dependsOn apiDocsProject.tasks.named('asciidoctor')

        // 생성된 문서를 JAR 내부의 정적 리소스 경로로 복사
        from (apiDocsProject.layout.buildDirectory.dir('docs/asciidoc')) {
            into 'static/docs'
        }
    }
}