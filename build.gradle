plugins {
    id 'java-library'
    id 'org.springframework.boot' apply(false)
    id 'io.spring.dependency-management'
    id 'io.spring.javaformat' apply(false)
    id 'org.asciidoctor.jvm.convert' apply(false)
}

apply from: 'lint.gradle'

allprojects {
    group = "${projectGroup}"
    version = "${applicationVersion}"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.asciidoctor.jvm.convert'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependenciesVersion}"
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.42")
        annotationProcessor("org.projectlombok:lombok:1.18.42")
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation 'org.springframework.boot:spring-boot-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.assertj:assertj-core'
        testImplementation 'org.mockito:mockito-core'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(project.javaVersion)
        }
    }

    bootJar.enabled = false
    jar.enabled = true

    test {
        useJUnitPlatform {
            excludeTags 'develop', 'restdocs'
        }
    }

    testing {
        suites {
            test {
                targets {
                    unitTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                excludeTags 'develop', 'context', 'restdocs'
                            }
                        }
                    }

                    contextTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                includeTags 'context'
                            }
                        }
                    }

                    restDocsTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                includeTags 'restdocs'
                            }
                        }
                    }

                    developTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                includeTags 'develop'
                            }
                        }
                    }
                }
            }
        }
    }
}

// --- 오케스트레이션 코드 (수정 부분) ---

// 1. 문서 생성 모듈 (tests:api-docs)
project(':tests:api-docs') {
    ext {
        // 스니펫이 생성될 경로
        snippetsDir = layout.buildDirectory.dir('generated-snippets')
    }

    // restDocsTest 태스크가 snippetsDir에 결과를 저장하도록 설정
    tasks.named('restDocsTest') {
        outputs.dir snippetsDir
    }

    tasks.named('asciidoctor') {
        dependsOn restDocsTest
        inputs.dir snippetsDir

        // index.adoc에서 스니펫을 참조할 수 있도록 설정
        attributes 'snippets': snippetsDir.get().asFile

        sources {
            include '**/index.adoc'
        }

        baseDirFollowsSourceDir()
    }
}

// 2. 실제 배포 모듈 (core-api)
project(':core:core-api') {
    // core-api는 실행 가능한 jar를 만들어야 하므로 bootJar 활성화
    bootJar {
        enabled = true

        def apiDocsProject = project(':tests:api-docs')
        // 빌드 전 문서 생성 선행
        dependsOn apiDocsProject.tasks.named('asciidoctor')

        // 생성된 HTML 문서를 JAR 내부의 정적 리소스 경로(static/docs)로 복사
        from(apiDocsProject.layout.buildDirectory.dir('docs/asciidoc')) {
            into 'static/docs'
        }
    }
}