plugins {
    id 'java-library'
    id 'org.springframework.boot' apply(false)
    id 'io.spring.dependency-management'
    id 'io.spring.javaformat' apply(false)
    id 'org.asciidoctor.jvm.convert' apply(false)
}

apply from: 'lint.gradle'

allprojects {
    group = "${projectGroup}"
    version = "${applicationVersion}"

    repositories {
        maven {
            url = project.findProperty('NEXUS_URL') ?: "http://211.220.195.12:5001/repository/nexsol-repo/"
            allowInsecureProtocol = true // HTTP 허용 설정
            credentials {
                username = project.findProperty('NEXUS_USERNAME')
                password = project.findProperty('NEXUS_PASSWORD')
            }
        }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.asciidoctor.jvm.convert'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependenciesVersion}"
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.42")
        annotationProcessor("org.projectlombok:lombok:1.18.42")
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation 'org.springframework.boot:spring-boot-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.assertj:assertj-core'
        testImplementation 'org.mockito:mockito-core'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(project.javaVersion)
        }
    }

    bootJar.enabled = false
    jar.enabled = true

    test {
        useJUnitPlatform {
            excludeTags 'develop', 'restdocs'
        }
    }

    testing {
        suites {
            test {
                targets {
                    unitTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                excludeTags 'develop', 'context', 'restdocs'
                            }
                        }
                    }

                    contextTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                includeTags 'context'
                            }
                        }
                    }

                    restDocsTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                includeTags 'restdocs'
                            }
                        }
                    }

                    developTest {
                        testTask.configure {
                            group = 'verification'
                            useJUnitPlatform {
                                includeTags 'develop'
                            }
                        }
                    }
                }
            }
        }
    }
    // --- 문서화 관련 설정 (수정 및 보강된 로직) ---
    tasks.named('asciidoctor') {
        dependsOn restDocsTest
        inputs.dir file('build/generated-snippets')
    }
    bootJar {
        dependsOn asciidoctor

        from("${asciidoctor.outputDir}"){
            into 'static/docs'
        }
    }

    tasks.register('copyDocsToResources', Copy) {
        dependsOn asciidoctor
        from asciidoctor.outputDir
        into file("build/resources/main/static/docs") // 실행되는 클래스패스 위치
    }

    tasks.named('bootRun') {
        dependsOn 'copyDocsToResources'
    }
}